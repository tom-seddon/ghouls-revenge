                .include "gcommon.s65"

;-------------------------------------------------------------------------

*=$70
                .dsection zp
                .cerror *>$90

*=$1900
                .dsection code
                .cerror *>gedmc_levels_org

;-------------------------------------------------------------------------

                .section zp
                .endsection

;-------------------------------------------------------------------------

                .section code
main:
                ; ; CLOSE #0
                ; lda #0
                ; tay
                ; jsr osfind

                lda #$77
                jsr osbyte

                ; disable shadow RAM
                lda #114
                ldx #1
                ldy #0
                jsr osbyte

                ;
                lda #$ea
                ldx #$00
                ldy #$ff
                jsr osbyte
                cpx #$00
                beq copro_ok

                brk
                .byte 255
                .text 'Not compatible with a second processor'
                brk

copro_ok:
                jsr define_udgs

                ; ESCAPE = ASCII 27
                lda #229
                ldx #1
                jsr osbyte
                
                ldx #0
                lda #0
clear_loop:
store:
                sta gedmc_levels_org+level_data_pages*256,x
                inx
                bne store
                inc store+2
                bpl clear_loop

                lda #22
                jsr oswrch
                lda #2
                jsr oswrch

                ; cursor off, display off, no interlace
                lda #8
                sta $fe00
                lda #%11110000
                sta $fe01

                ; title screen
                ldx #<run_gscrp
                ldy #>run_gscrp
                jsr oscli

                ;
crtc_addr=$2b00>>3
                lda #12
                sta $fe00
                lda #>crtc_addr
                sta $fe01

                lda #13
                sta $fe00
                lda #<crtc_addr
                sta $fe01

                lda #19
                jsr osbyte

                ; cursor off, display on, no interlace
                lda #8
                sta $fe00
                lda #%11000000
                sta $fe01

                jsr menu_getch

                lda #19
                jsr osbyte

                ldx #0
print_loop:
                lda text,x
                jsr oswrch
                inx
                cpx #size(text)
                bne print_loop

key_loop:
                jsr menu_getch

                cmp #'V'
                beq version
                cmp #'v'
                beq version
                
                jmp key_loop

                rts

menu_getch:
                lda #15
                ldx #0
                jsr osbyte

                jsr osrdch

                cmp #'1'
                beq play

                cmp #'2'
                beq edit

                cmp #'3'
                beq debug

                rts

version:
                ldx #0
-
                lda version_text,x
                jsr oswrch
                inx
                cpx #size(version_text)
                bne -
                jmp key_loop

play:
                lda #$00
                sta $0281       ; AUG p117
                jsr gdummy
gmenu:
                ldx #<run_gmenu
                ldy #>run_gmenu
                jmp oscli

debug:
                lda #$01
                sta $0281       ; AUG p117
                jsr gdummy
                jmp gmenu

edit:
                jsr gdummy
                ldx #<run_gedmc
                ldy #>run_gedmc
                jmp oscli

gdummy:
                ldx #<run_gdummy
                ldy #>run_gdummy
                jsr oscli

                lda #19
                jsr osbyte

                ; cursor off, display off, no interlace
                lda #8
                sta $fe00
                lda #%11110000
                sta $fe01

                rts
                
run_gmenu:
                .text '/GMENU',13
                
run_gedmc:
                .text '/GEDMC',13
                
run_gscrp:
                .text "/GSCRP",13

run_gdummy:
                .text '/GDUMMY',13

; 01234567890123456789
; V1.01  ID: 20241015-
; 000458-90730ec

                ; Y-2 in each case due to the different screen start
                ; address
                
version_text: .block
                .cerror len(version_major)!=1
                .cerror len(version_minor)!=2
                .text 31,14,29,17,6,'V',version_major,'.',version_minor
                .endblock
                
text: .block
                .text 31,10,25,'          '
                .text 31,10,26,17,7,' 1. ',17,3,'PLAY  '
                .text 31,10,27,17,7,' 2. ',17,1,'EDIT  '
                .text 31,10,28,17,7,' 3. ',17,5,'DEBUG '
                .text 31,10,29,'          '
                .endblock

define_udgs:
                .include 'gudgs_code.s65'
                
                .endsection

;-------------------------------------------------------------------------
